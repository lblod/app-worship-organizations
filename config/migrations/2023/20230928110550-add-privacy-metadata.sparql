
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX erediensten: <http://data.lblod.info/vocabularies/erediensten/>
PREFIX org: <http://www.w3.org/ns/org#>

# Persist the privacyGraph in the organization graph
# This allow the privacy service to store metadata about the reason of accessing private data
# In the future, private data should be stored there (maybe we can reroute directly with the reasoner?)
INSERT {
  GRAPH ?orgGraph {
    ?privacyGraph a ext:PrivacyGraph;
                  mu:uuid ?uuidBestuurseenheid;
                  ext:hasGraph ?hasGraph.
  }
} WHERE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?persoon a foaf:Person;
      mu:uuid ?uuidPersoon;
      foaf:firstName ?classificationLabel;
      foaf:familyName ?naam;
      foaf:member ?bestuurseenheid ;
      foaf:account ?account.
    ?account a foaf:OnlineAccount;
      mu:uuid ?uuidAccount;
      foaf:accountServiceHomepage <https://github.com/lblod/mock-login-service>;
      ext:sessionRole "LoketLB-eredienstOrganisatiesGebruiker".

    BIND( STRAFTER( STR(?bestuurseenheid), "http://data.lblod.info/id/bestuurseenheden/" ) as ?uuidBestuurseenheid )
    BIND(IRI(CONCAT("http://mu.semte.ch/graphs/organizations/", ?uuidBestuurseenheid)) AS ?orgGraph)
    BIND(IRI(CONCAT("http://mu.semte.ch/graphs/privacygraphs/", ?uuidBestuurseenheid)) AS ?hasGraph)
    BIND(IRI(CONCAT("http://data.lblod.info/id/person-information-graphs/", ?uuidBestuurseenheid)) AS ?privacyGraph)
  }
}

