PREFIX ere: <http://data.lblod.info/vocabularies/erediensten/>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX organisatie: <https://data.vlaanderen.be/ns/organisatie#>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX generiek: <https://data.vlaanderen.be/ns/generiek#>

DELETE {
  GRAPH <http://mu.semte.ch/graphs/tmp-graph-to-import-services> {
    ?primarySite ?pprimarySite ?oprimarySite .
    ?address ?paddress ?oaddress .
    ?contactPoint ?pcontactPoint ?ocontactPoint .
  }
} INSERT {
  GRAPH ?g {
    ?primarySite ?pprimarySite ?oprimarySite .
    ?address ?paddress ?oaddress .
    ?contactPoint ?pcontactPoint ?ocontactPoint .
  }
} WHERE {
  GRAPH ?g {
    VALUES ?type {
      ere:BestuurVanDeEredienst
      ere:CentraalBestuurVanDeEredienst
    }
    ?worshipAdminUnit a ?type ;
      org:hasPrimarySite ?primarySite .
  }

  GRAPH <http://mu.semte.ch/graphs/tmp-graph-to-import-services> {
    ?primarySite organisatie:bestaatUit ?address ;
      ?pprimarySite ?oprimarySite .
      
    ?address ?paddress ?oaddress .

    OPTIONAL {
      ?primarySite org:siteAddress ?contactPoint .
      ?contactPoint ?pcontactPoint ?ocontactPoint .
    }
  }
  FILTER (?g != <http://mu.semte.ch/graphs/tmp-graph-to-import-services>)
}
