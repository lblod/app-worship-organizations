PREFIX ere: <http://data.lblod.info/vocabularies/erediensten/>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX organisatie: <https://data.vlaanderen.be/ns/organisatie#>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX generiek: <https://data.vlaanderen.be/ns/generiek#>

DELETE {
  GRAPH <http://mu.semte.ch/graphs/tmp-graph-to-import-services> {
    ?mandate ?pmandate ?omandate .
  }
} INSERT {
  GRAPH ?g {
    ?mandate ?pmandate ?omandate .
  }
} WHERE {
  GRAPH ?g {
    VALUES ?type {
      ere:BestuurVanDeEredienst
      ere:CentraalBestuurVanDeEredienst
    }
    ?worshipAdminUnit a ?type.
  }

  GRAPH <http://mu.semte.ch/graphs/tmp-graph-to-import-services> {
    ?orgaan besluit:bestuurt ?worshipAdminUnit .

    ?orgaanInTijd generiek:isTijdspecialisatieVan ?orgaan ;
      <http://www.w3.org/ns/org#hasPost> ?mandate .

    ?mandate ?pmandate ?omandate .
  }
  FILTER (?g != <http://mu.semte.ch/graphs/tmp-graph-to-import-services>)
}
