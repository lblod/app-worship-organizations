# RULE THREE
# Provinces will see the worship services which they have a link
# (any link) to in the module 'betrokken lokale besturen'

PREFIX ere: <http://data.lblod.info/vocabularies/erediensten/>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX organisatie: <https://data.vlaanderen.be/ns/organisatie#>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX generiek: <https://data.vlaanderen.be/ns/generiek#>

INSERT {
  GRAPH ?orgGraph {
    ?worshipAdminUnit ?pworshipAdminUnit ?oworshipAdminUnit .
    ?betrokke ?pbetrokke ?obetrokke .
  }
} WHERE {
  GRAPH <http://mu.semte.ch/graphs/tmp-graph-to-import-services> {
    ?s a besluit:Bestuurseenheid ;
      mu:uuid ?uuidBestuurseenheid ;
      org:classification <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/5ab0e9b8a3b2ca7c5e000000> ; # Provincie
      ere:betrokkenBestuur ?betrokke .

    ?betrokke org:organization ?worshipAdminUnit ;
      ?pbetrokke ?obetrokke .

    ?worshipAdminUnit ?pworshipAdminUnit ?oworshipAdminUnit .

    BIND(IRI(CONCAT("http://mu.semte.ch/graphs/organizations/", ?uuidBestuurseenheid, "/LoketLB-eredienstOrganisatiesGebruiker")) AS ?orgGraph)
  }
}
